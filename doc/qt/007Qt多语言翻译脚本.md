
ts.bat生成ts
```
@echo off
setlocal EnableDelayedExpansion

set "folder=%~dp0\..\%1"

SETLOCAL ENABLEDELAYEDEXPANSION

for /r "%folder%" %%f in (*.h *.hpp) do (
  set "headers=!headers!%%~ff "
)

for /r "%folder%" %%f in (*.cpp *.cc) do (
  set "srcs=!srcs!%%~ff "
)

set "outfolder=%~dp0\..\%1\lang"
if not exist "%outfolder%" (
    mkdir "%outfolder%"
)

if "%2"=="" (
for /r "%folder%" %%f in (*.ts) do (
  echo lupdate %%~nxf
  lupdate -recursive !headers! !srcs! -ts %outfolder%\%%~nxf
)
) else (
	echo lupdate %1_%2.ts
	lupdate -recursive !headers! !srcs! -ts %outfolder%\%1_%2.ts
)

pause

```

qm.bat生成qm
```
@echo off
setlocal EnableDelayedExpansion

set "folder=%~dp0\..\%1\lang"
set "debug=%~dp0\..\bin\x64\Debug\lang\%2"
set "release=%~dp0\..\bin\x64\Release\lang\%2"
set "res=%~dp0\..\%1\res\lang\%2"

cd %folder%

if "%2"=="" (
	for /r "%folder%" %%f in (*.ts) do (
	  echo lrelease %%~nxf
	  lrelease %%~nxf %%nf.qm
	)
) else (
	echo lrelease %1_%2.ts
	lrelease %1_%2.ts %1_%2.qm

	if not exist "%debug%" (
		mkdir "%debug%"
	)
	if not exist "%release%" (
		mkdir "%release%"
	)
	if not exist "%res%" (
		mkdir "%res%"
	)

	copy /B %1_%2.qm %debug%\%1_%2.qm
	copy /B %1_%2.qm %release%\%1_%2.qm
	copy /B %1_%2.qm %res%\%1_%2.qm

	del %1_%2.qm
)

pause

```

使用
ts.bat和qm.bat在目录tool下，tool目录与需要需要翻译的项目同一层级目录

在项目目录下新建lang目录，新建ts_zh.bat脚本文件
```
../../tool/ts.bat qtest zh
```

在项目目录下新建lang目录，新建qm_zh.bat脚本文件
```
../../tool/qm.bat qtest zh
```